
struct FixedHeap {
	coalesceHeap:CoalesceHeap,
	elementSize:uint32,
	elementCount:uint32,
	block:FixedHeap,
}

struct FixedHeapBlock {
	owner:FixedHeap,
	next:FixedHeapBlock,
	firstElement:FixedHeapElement,
}

struct FixedHeapElement {
	owner:FixedHeapBlock,
	allocatorType:uint32,
	next:FixedHeapElement,
}

function FixedHeapElementExtra():uint32 {
	return sizeof FixedHeapElement - 4;
}

function FixedHeapInit(heap:FixedHeap, coalesceHeap:CoalesceHeap, elementSize:uint32, elementCount:uint32):FixedHeap {
	(heap.coalesceHeap) = coalesceHeap;
	(heap.elementSize) = elementSize;
	(heap.elementCount) = elementCount;
	return heap;
}

function FixedHeapAddBlock(heap:FixedHeap):FixedHeap {
	var block:FixedHeapBlock;
	
	block = CoalesceHeapAlloc(
		(heap.coalesceHeap),
		(((heap.elementSize) + FixedHeapElementExtra()) * (heap.elementCount)) + sizeof FixedHeapBlock
	);
	FixedHeapInitBlock(heap, block);
	
	(block.next) = heap.block;
	(heap.block) = block;
	
	return heap;
}

function FixedHeapInitBlock(heap:FixedHeap, block:FixedHeapBlock):FixedHeapBlock {
	(block.owner) = heap;
	
	var element:FixedHeapElement;
	var next:FixedHeapElement;
	
	element = block + sizeof FixedHeapBlock;
	(block.firstElement) = element;
	
	var i:uint32;
	i = 0;
	while (i < (heap.elementCount)) {
		next = element + (heap.elementSize) + FixedHeapElementExtra();
		(element.owner) = block;
		(element.allocatorType) = allocatorTypeFixedHeap();
		(element.next) = next;
		element = next;
		i = i + 1;
	}
	
	// Last doesn't have a next.
	element = element - ((heap.elementSize) + FixedHeapElementExtra());
	(element.next) = 0;
	
	return block;
}

function FixedHeapAllocSingleBlock(block:FixedHeapBlock):Ptr {
	if ((block.firstElement) == 0) {
		return 0;
	}
	
	var result:Ptr;
	result = (block.firstElement) + FixedHeapElementExtra();
	
	(block.firstElement) = (block.firstElement).next;
	
	return result;
}

function FixedHeapAllocNoBlockAlloc(block:FixedHeapBlock):Ptr {
	while (block != 0) {
		var result:Ptr;
		result = FixedHeapAllocSingleBlock(block);
		if (result != 0) {
			return result;
		}
		
		block = block.next;
	}
	return 0;
}

function FixedHeapAlloc(heap:FixedHeap):Ptr {
	var result:Ptr;
	result = FixedHeapAllocNoBlockAlloc(heap.block);
	if (result != 0) {
		return result;
	}
	
	FixedHeapAddBlock(heap);
	return FixedHeapAllocSingleBlock(heap.block);
}

function FixedHeapFree(ptr:Ptr):uint32 {
	var element:FixedHeapElement;
	element = ptr - FixedHeapElementExtra();
	var block:FixedHeapBlock;
	block = element.owner;
	(element.next) = block.firstElement;
	(block.firstElement) = element;
	return 0;
}
